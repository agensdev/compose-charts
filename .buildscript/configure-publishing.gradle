def chartsGroupId = "hu.ma.compose"
def chartsArtifactId = "charts"
def chartsVersion = "0.1.4"

afterEvaluate {
  publishing {
    publications {
      release(MavenPublication) {
        from components.release
        artifact sourceJar

        group = chartsGroupId
        artifactId = chartsArtifactId
        version = chartsVersion

        setupPublicationPom(it)
      }
    }

    setupPublicationRepository(it)
  }
}

task sourceJar(type: Jar) {
  from android.sourceSets.main.java.srcDirs
  classifier "source"
}

void setupPublicationPom(Publication publication) {
  publication.pom {
    licenses {
      license {
        name = "The Apache License, Version 2.0"
        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
      }
    }

    developers {
      developer {
        id = "humawork"
        name = "Huma"
      }
    }

    scm {
      url = "https://github.com/humawork/compose-charts"
      connection = "scm:git:git://github.com/humawork/compose-charts.git"
      developerConnection = "scm:git:git://github.com/humawork/compose-charts.git"
    }
  }
}

void setupPublicationRepository(PublishingExtension publishing) {
  publishing.repositories {
    maven {
      url = "https://api.bintray.com/maven/humawork/maven/compose-charts/;publish=0;override=1"
      credentials {
        username = "humawork"
        password = findProperty("bintray_api_key").toString()
      }
    }
  }
}
